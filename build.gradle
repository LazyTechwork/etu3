plugins {
    id 'java'
    id 'jacoco'
    id 'org.barfuin.gradle.jacocolog' version '2.0.0'
}

group 'me.lazytechwork.algods'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation 'org.knowm.xchart:xchart:3.8.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()

    filter {
        excludeTestsMatching "me.lazytechwork.algods.benchmarking.*"
    }

    finalizedBy jacocoTestReport
}

task benchmark(type: Test) {
    useJUnitPlatform()
    filter {
        includeTestsMatching "me.lazytechwork.algods.benchmarking.*"
    }
}

jacoco {
    reportsDir = file("$buildDir/jacoco")
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled true
        xml.destination file("${rootProject.projectDir}/build/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(
                    dir: it,
                    exclude: ['me/lazytechwork/core/**']
            )
        })
    }
}
